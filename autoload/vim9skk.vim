vim9script

# ã‚¹ã‚¯ãƒªãƒ—ãƒˆãƒ­ãƒ¼ã‚«ãƒ«å¤‰æ•° {{{
const MODE_HIRA = 1
const MODE_KATA = 2
const MODE_HANK = 3
const MODE_ALPH = 4
const MODE_ABBR = 5

const SKKMODE_DIRECT = 0
const SKKMODE_MIDASI = 1
const SKKMODE_SELECT = 2

const POPUPWIN_KIND_NONE = 0
const POPUPWIN_KIND_MODE = 1
const POPUPWIN_KIND_KOUHO = 2

var initialized = false
var mode = { id: MODE_HIRA, use_roman: true, items: [] }
var skkmode = SKKMODE_DIRECT
var start_pos = 0
var end_pos = 1
var pos_delta = 0 # ç¢ºå®šå‰å¾Œã®ã‚«ãƒ¼ã‚½ãƒ«ä½ç½®ã®å·®ç•°
var henkan_key = ''
var okuri = ''
var kouho = []
var kouho_index = -1
var last_word = ''
var jisyo = {}
var recent_jisyo = {}
var chain_jisyo = {}
var popupwin_winid = 0
var popupwin_kind = POPUPWIN_KIND_NONE
var popupwin_midasi = 0
var popupwin_midasi_update_timer = 0
var popupwin_midasi_pos = {}
var is_registering_user_jisyo = false

var roman_table = {
  # 4æ–‡å­—
  ltsu: 'ã£', xtsu: 'ã£',
  # 3æ–‡å­—
  gya: 'ãã‚ƒ', gyi: 'ããƒ', gyu: 'ãã‚…', gye: 'ãã‡', gyo: 'ãã‚‡',
  zya: 'ã˜ã‚ƒ', zyi: 'ã˜ãƒ', zyu: 'ã˜ã‚…', zye: 'ã˜ã‡', zyo: 'ã˜ã‚‡',
  dya: 'ã¢ã‚ƒ', dyi: 'ã§ãƒ', dyu: 'ã¢ã‚…', dye: 'ã¢ã‡', dyo: 'ã¢ã‚‡',
  dha: 'ã¢ã‚ƒ', dhi: 'ã§ãƒ', dhu: 'ã¢ã‚…', dhe: 'ã¢ã‡', dho: 'ã¢ã‚‡',
  bya: 'ã³ã‚ƒ', byi: 'ã³ãƒ', byu: 'ã³ã‚…', bye: 'ã³ã‡', byo: 'ã³ã‚‡',
  pya: 'ã´ã‚ƒ', pyi: 'ã´ãƒ', pyu: 'ã´ã‚…', pye: 'ã´ã‡', pyo: 'ã´ã‚‡',
  kya: 'ãã‚ƒ', kyi: 'ããƒ', kyu: 'ãã‚…', kye: 'ãã‡', kyo: 'ãã‚‡',
  sya: 'ã—ã‚ƒ', syi: 'ã—ãƒ', syu: 'ã—ã‚…', sye: 'ã—ã‡', syo: 'ã—ã‚‡',
  sha: 'ã—ã‚ƒ', shi: 'ã—',   shu: 'ã—ã‚…', she: 'ã—ã‡', sho: 'ã—ã‚‡',
  tya: 'ã¡ã‚ƒ', tyi: 'ã¡',   tyu: 'ã¡ã‚…', tye: 'ã¡ã‡', tyo: 'ã¡ã‚‡',
  cha: 'ã¡ã‚ƒ', chi: 'ã¡ãƒ', chu: 'ã¡ã‚…', che: 'ã¡ã‡', cho: 'ã¡ã‚‡',
  tsa: 'ã¤ã', tsi: 'ã¤ãƒ', tsu: 'ã¤',   tse: 'ã¤ã‡', tso: 'ã¤ã‰',
  tha: 'ã¦ã‚ƒ', thi: 'ã¦ãƒ', thu: 'ã¦ã‚…', the: 'ã¦ã‡', tho: 'ã¦ã‚‡',
  nya: 'ã«ã‚ƒ', nyi: 'ã«ãƒ', nyu: 'ã«ã‚…', nye: 'ã«ã‡', nyo: 'ã«ã‚‡',
  hya: 'ã²ã‚ƒ', hyi: 'ã²ãƒ', hyu: 'ã²ã‚…', hye: 'ã²ã‡', hyo: 'ã²ã‚‡',
  mya: 'ã¿ã‚ƒ', myi: 'ã¿ãƒ', myu: 'ã¿ã‚…', mye: 'ã¿ã‡', myo: 'ã¿ã‚‡',
  rya: 'ã‚Šã‚ƒ', ryi: 'ã‚Šãƒ', ryu: 'ã‚Šã‚…', rye: 'ã‚Šã‡', ryo: 'ã‚Šã‚‡',
  lya: 'ã‚ƒ', lyu: 'ã‚…', lyo: 'ã‚‡', ltu: 'ã£', lwa: 'ã‚',
  xya: 'ã‚ƒ', xyu: 'ã‚…', xyo: 'ã‚‡', xtu: 'ã£', xwa: 'ã‚',
  # 2æ–‡å­—
  cc: 'ã£c',
  ja: 'ã˜ã‚ƒ', ji: 'ã˜', ju: 'ã˜ã‚…', je: 'ã˜ã‡', jo: 'ã˜ã‚‡', jj: 'ã£j',
  fa: 'ãµã', fi: 'ãµãƒ', fu: 'ãµ', fe: 'ãµã‡', fo: 'ãµã‰', ff: 'ã£f',
  va: 'ã‚”ã', vi: 'ã‚”ãƒ', vu: 'ã‚”', ve: 'ã‚”ã‡', vo: 'ã‚”ã‰',
  la: 'ã', li: 'ãƒ', lu: 'ã…', le: 'ã‡', lo: 'ã‰',
  xa: 'ã', xi: 'ãƒ', xu: 'ã…', xe: 'ã‡', xo: 'ã‰',
  ga: 'ãŒ', gi: 'ã', gu: 'ã', ge: 'ã’', go: 'ã”', gg: 'ã£g',
  za: 'ã–', zi: 'ã˜', zu: 'ãš', ze: 'ãœ', zo: 'ã', zz: 'ã£z',
  da: 'ã ', di: 'ã¢', du: 'ã¥', de: 'ã§', do: 'ã©', dd: 'ã£d',
  ba: 'ã°', bi: 'ã³', bu: 'ã¶', be: 'ã¹', bo: 'ã¼', bb: 'ã£b',
  pa: 'ã±', pi: 'ã´', pu: 'ã·', pe: 'ãº', po: 'ã½', pp: 'ã£p',
  ka: 'ã‹', ki: 'ã', ku: 'ã', ke: 'ã‘', ko: 'ã“', kk: 'ã£k',
  sa: 'ã•', si: 'ã—', su: 'ã™', se: 'ã›', so: 'ã', ss: 'ã£s',
  ta: 'ãŸ', ti: 'ã¡', tu: 'ã¤', te: 'ã¦', to: 'ã¨', tt: 'ã£t',
  na: 'ãª', ni: 'ã«', nu: 'ã¬', ne: 'ã­', no: 'ã®',
  ha: 'ã¯', hi: 'ã²', hu: 'ãµ', he: 'ã¸', ho: 'ã»', hh: 'ã£h',
  ma: 'ã¾', mi: 'ã¿', mu: 'ã‚€', me: 'ã‚', mo: 'ã‚‚', mm: 'ã£m',
  ya: 'ã‚„', yi: 'ã‚', yu: 'ã‚†', ye: 'ã‚‘', yo: 'ã‚ˆ', yy: 'ã£y',
  ra: 'ã‚‰', ri: 'ã‚Š', ru: 'ã‚‹', re: 'ã‚Œ', ro: 'ã‚', rr: 'ã£r',
  wa: 'ã‚', wo: 'ã‚’', nn: 'ã‚“',
  # 1æ–‡å­—
  a: 'ã‚', i: 'ã„', u: 'ã†', e: 'ãˆ', o: 'ãŠ', n: 'ã‚“',
  # è¨˜å·
  'z ': 'ã€€', 'z.': 'â€¦', 'z/': 'ãƒ»', 'z[': 'ã€Œ', 'z]': 'ã€',
  zl: 'â†’', zh: 'â†', zj: 'â†“', zk: 'â†‘',
  '-': 'ãƒ¼', '.': 'ã€‚', ',': 'ã€', '!': 'ï¼', '?': 'ï¼Ÿ', '/': 'ãƒ»', '~': 'ï½',
}
# Init()ã§ä½œã‚‹
#const roman_table_items = roman_table->items()
var roman_table_items = []

# {ã‹:'k'}ã¿ãŸã„ãªdict
# å¤‰æ›æ™‚ã«ã€Œã‘ã‚“ã•ã*ã™ã‚‹ã€â†’ã€Œã‘ã‚“ã•ãsã€ã¨ã„ã†é¢¨ã«è¾æ›¸ã‚’æ¤œç´¢ã™ã‚‹æ™‚ã«ä½¿ã†
# Init()ã§ä½œã‚‹
var okuri_table = {}

const hira_chars = ('ãã‚ãƒã„ã…ã†ã‡ãˆã‰ãŠã‹ãŒããããã‘ã’ã“ã”' ..
  'ã•ã–ã—ã˜ã™ãšã›ãœãããŸã ã¡ã¢ã£ã¤ã¥ã¦ã§ã¨ã©' ..
  'ãªã«ã¬ã­ã®ã¯ã°ã±ã²ã³ã´ãµã¶ã·ã¸ã¹ãºã»ã¼ã½' ..
  'ã¾ã¿ã‚€ã‚ã‚‚ã‚ƒã‚„ã‚…ã‚†ã‚‡ã‚ˆã‚‰ã‚Šã‚‹ã‚Œã‚ã‚ã‚ã‚ã‚‘ã‚’ã‚“ã‚”ãƒ¼ã€€')->split('.\zs')

const kata_chars = ('ã‚¡ã‚¢ã‚£ã‚¤ã‚¥ã‚¦ã‚§ã‚¨ã‚©ã‚ªã‚«ã‚¬ã‚­ã‚®ã‚¯ã‚°ã‚±ã‚²ã‚³ã‚´' ..
  'ã‚µã‚¶ã‚·ã‚¸ã‚¹ã‚ºã‚»ã‚¼ã‚½ã‚¾ã‚¿ãƒ€ãƒãƒ‚ãƒƒãƒ„ãƒ…ãƒ†ãƒ‡ãƒˆãƒ‰' ..
  'ãƒŠãƒ‹ãƒŒãƒãƒãƒãƒãƒ‘ãƒ’ãƒ“ãƒ”ãƒ•ãƒ–ãƒ—ãƒ˜ãƒ™ãƒšãƒ›ãƒœãƒ' ..
  'ãƒãƒŸãƒ ãƒ¡ãƒ¢ãƒ£ãƒ¤ãƒ¥ãƒ¦ãƒ§ãƒ¨ãƒ©ãƒªãƒ«ãƒ¬ãƒ­ãƒ®ãƒ¯ãƒ°ãƒ±ãƒ²ãƒ³ãƒ´ãƒ¼ã€€')->split('.\zs')

const hankaku_chars = ('ï½§ï½±ï½¨ï½²ï½©ï½³ï½ªï½´ï½«ï½µï½¶ï½¶ï¾ï½·ï½·ï¾ï½¸ï½¸ï¾ï½¹ï½¹ï¾ï½ºï½ºï¾' ..
  'ï½»ï½»ï¾ï½¼ï½¼ï¾ï½½ï½½ï¾ï½¾ï½¾ï¾ï½¿ï½¿ï¾ï¾€ï¾€ï¾ï¾ï¾ï¾ï½¯ï¾‚ï¾‚ï¾ï¾ƒï¾ƒï¾ï¾„ï¾„ï¾' ..
  'ï¾…ï¾†ï¾‡ï¾ˆï¾‰ï¾Šï¾Šï¾ï¾Šï¾Ÿï¾‹ï¾‹ï¾ï¾‹ï¾Ÿï¾Œï¾Œï¾ï¾Œï¾Ÿï¾ï¾ï¾ï¾ï¾Ÿï¾ï¾ï¾ï¾ï¾Ÿ' ..
  'ï¾ï¾ï¾‘ï¾’ï¾“ï½¬ï¾”ï½­ï¾•ï½®ï¾–ï¾—ï¾˜ï¾™ï¾šï¾›ï¾œï¾œï½²ï½´ï½¦ï¾ï½³ï¾ï½° ')->split('.[ï¾Ÿï¾]\?\zs')

const alphabet_chars = ('ï¼ï¼‘ï¼’ï¼“ï¼”ï¼•ï¼–ï¼—ï¼˜ï¼™' ..
  'ï½ï½‚ï½ƒï½„ï½…ï½†ï½‡ï½ˆï½‰ï½Šï½‹ï½Œï½ï½ï½ï½ï½‘ï½’ï½“ï½”ï½•ï½–ï½—ï½˜ï½™ï½š' ..
  'ï¼¡ï¼¢ï¼£ï¼¤ï¼¥ï¼¦ï¼§ï¼¨ï¼©ï¼ªï¼«ï¼¬ï¼­ï¼®ï¼¯ï¼°ï¼±ï¼²ï¼³ï¼´ï¼µï¼¶ï¼·ï¼¸ï¼¹ï¼º' ..
  'ã€€ï¼ï¼‚ï¼ƒï¼„ï¼…ï¼†ï¼‡ï¼ˆï¼‰ï¼ï¼¾ï¼¼ï¼ ï¼»ï¼›ï¼šï¼½ï¼Œï¼ï¼ï¼¼ï¼ï½ï½œï½€ï½›ï¼‹ï¼Šï½ï¼œï¼ï¼Ÿï¼¿')->split('.\zs')

const abbr_chars = ('0123456789' ..
  'abcdefghijklmnopqrstuvwxyz' ..
  'ABCDEFGHIJKLMNOPQRSTUVWXYZ' ..
  ' !"#$%&''()-^\@[;:],./\=~|`{+*}<>?_')->split('.\zs')
# }}}

# ãƒ¦ãƒ¼ãƒ†ã‚£ãƒªãƒ†ã‚£ãƒ¼ {{{
# tr()ã‚’ä½¿ã„ãŸã„ã‘ã©ã€åŠè§’ã‚«ãƒŠæ¿ç‚¹åŠæ¿ç‚¹ã«å¯¾å¿œã—ãªã„ã¨ã„ã‘ãªã„ã®ã§è‡ªä½œ
# åŠè§’ã‚«ãƒŠâ†’ã²ã‚‰ãŒãªorã‚«ã‚¿ã‚«ãƒŠã¯NG
def ConvChars(str: string, from_chars: list<string>, to_chars: list<string>): string
  var dest = []
  for c in str->split('.\zs')
    const i = from_chars->index(c)
    dest += [i ==# - 1 ? c : to_chars[i]]
  endfor
  return dest->join('')
enddef

def SwapChars(str: string, a: list<string>, b: list<string>): string
  return str->ConvChars(a + b, b + a)
enddef

def GetLine(): string
  return mode() ==# 'c' ? getcmdline() : getline('.')
enddef

def GetPos(): number
  return mode() ==# 'c' ? getcmdpos() : col('.')
enddef

def AddStr(a: string, b: string): string
  return a .. b
enddef

def StartsWith(str: string, expr: string): bool
  return str->strcharpart(0, expr->strchars()) ==# expr
enddef

# æ–‡å­—åˆ—ã‚’å¿…ãš2ã¤ã«åˆ†å‰²ã™ã‚‹
def Split(str: string, dlm: string): list<string>
  const i = str->stridx(dlm)
  return i ==# - 1 ? [str, ''] : [str->strpart(0, i), str->strpart(i + 1)]
enddef

# é †ç•ªã‚’ä¿ã£ãŸã¾ã¾uniqã™ã‚‹
def Uniq(list: list<any>): list<any>
  var result = []
  for a in list
    if result->Excludes(a)
      result->add(a)
    endif
  endfor
  return result
enddef

def ToItems(list: list<any>, CreateKeyValueFunc: func): list<list<any>>
  var d = {}
  for i in list
    const [k, v] = CreateKeyValueFunc(i)
    d[k] = v
  endfor
  return d->items()
enddef

def Includes(a: list<any>, b: any): bool
  return a->index(b) !=# -1
enddef

def Excludes(a: list<any>, b: any): bool
  return a->index(b) ==# -1
enddef

def ForEach(a: list<any>, F: func)
  for v in a
    if !!v
      F(v)
    endif
  endfor
enddef

def DoUserEvent(event: string)
  if exists($'#User#{event}')
    execute $'doautocmd User {event}'
  endif
enddef

def GetPopupWinPos(d: number = 1): any
  var pp = {}
  if mode() ==# 'c'
    const p = getcmdscreenpos()
    pp = {
      col: p % &columns,
      line: &lines + p / &columns - d - &cmdheight + 1,
    }
  else
    const c = getcurpos()
    const p = screenpos(0, c[1], c[2])
    pp = {
      col: p.col,
      line: min([p.row + d, &lines]),
    }
  endif
  pp = g:vim9skk.change_popuppos(pp)
  return {
    pos: 'topleft',
    col: pp.col,
    line: pp.line,
    wrap: false,
  }
enddef

export def NoChangePopupPos(popup_pos: any): any
  return popup_pos
enddef

# }}}

# è¡¨ç¤ºåˆ¶å¾¡ {{{
def ClosePopupWin()
  if !!popupwin_winid
    popup_close(popupwin_winid)
    popupwin_winid = 0
    popupwin_kind = POPUPWIN_KIND_NONE
    kouho_index = -1
  endif
enddef
# }}}

# åŸºæœ¬ {{{
def Init()
  DoUserEvent('Vim9skkInitPre')
  augroup vim9skk
    autocmd!
    autocmd BufEnter * MapToBuf()
    autocmd InsertEnter * OnInsertEnter()
    autocmd InsertLeavePre * OnInsertLeavePre()
    autocmd CmdlineEnter * OnCmdlineEnter()
    autocmd CmdlineLeave * OnCmdlineLeavePre()
    autocmd VimLeave * SaveRecentJisyo()
    # Note:
    # ç¢ºå®šæ™‚ã®<BS>ã«ã‚ˆã‚‹ã‚«ãƒ¼ã‚½ãƒ«ç§»å‹•ã§ç™ºç«ã™ã‚‹ã¨è‰²ã€…é¢å€’ãªã®ã§SafeStateã‚’æŒŸã‚€
    autocmd CursorMovedI,CursorMovedC * autocmd SafeState * ++once FollowCursorModePopupWin()
    autocmd ColorScheme * ColorScheme()
  augroup END
  # ãƒ¦ãƒ¼ã‚¶ãƒ¼å®šç¾©ã®ãƒ­ãƒ¼ãƒå­—å…¥åŠ›ã‚’è¿½åŠ 
  roman_table->extend(g:vim9skk.roman_table)
  roman_table_items = roman_table->items()
  # é€ã‚Šä»®åãƒ†ãƒ¼ãƒ–ãƒ«ã‚’ä½œæˆ
  for [k, v] in roman_table_items
    okuri_table[v->strcharpart(0, 1)] = k[0]
  endfor
  # è¾æ›¸ã®ãƒ‘ã‚¹ã®ãƒ¯ã‚¤ãƒ«ãƒ‰ã‚«ãƒ¼ãƒ‰ã‚’å±•é–‹ã—ã¦ãŠã
  var expanded = []
  for j in g:vim9skk.jisyo
    const [path, enc] = ToFullPathAndEncode(j)
    for p in path->split('\n')
      expanded += [$'{p}:{enc}']
    endfor
  endfor
  g:vim9skk.jisyo = expanded
  # ãã®ä»–åˆæœŸåŒ–å‡¦ç†
  SetMode(MODE_HIRA)
  ColorScheme()
  initialized = true
enddef

def ColorScheme()
  silent! hi default vim9skkMidasi cterm=underline gui=underline
  silent! hi default link vim9skkModeOff PmenuExtra
  silent! hi default link vim9skkModeHira PmenuSel
  silent! hi default link vim9skkModeKata PmenuMatchSel
  silent! hi default link vim9skkModeHankaku Pmenu
  silent! hi default link vim9skkModeAlphabet Pmenu
  silent! hi default link vim9skkModeAbbr Pmenu
enddef

export def Enable()
  if g:vim9skk_enable
    return
  endif
  g:vim9skk_enable = true
  if !initialized
    Init()
  endif
  MapToBuf()
  ToDirectMode()
  RunOnMidasi()
  PopupMode()
  DoUserEvent('Vim9skkModeChanged')
  DoUserEvent('Vim9skkEnter')
enddef

export def Disable(popup_even_off: bool = true): string
  if !g:vim9skk_enable
    return ''
  endif
  if skkmode !=# SKKMODE_DIRECT
    Complete()->feedkeys('nit')
    ToDirectMode()
  endif
  g:vim9skk_enable = false
  UnmapAll()
  CloseKouho()
  PopupMode()
  DoUserEvent('Vim9skkModeChanged')
  DoUserEvent('Vim9skkLeave')
  return ''
enddef

export def ToggleSkk()
  if g:vim9skk_enable && !mode.use_roman
    SetMode(MODE_HIRA)
  elseif g:vim9skk_enable
    Disable()
  else
    Enable()
  endif
enddef

def OnInsertEnter()
  if g:vim9skk_enable
    PopupMode()
  endif
enddef

def OnInsertLeavePre()
  if !g:vim9skk_enable
    return
  endif
  CloseKouho()
  if skkmode ==# SKKMODE_DIRECT
    return
  endif
  const before = GetTarget()
  const after = before->RemoveMarker()
  setline('.', getline('.')->substitute(
    $'\%{start_pos}c{"."->repeat(strchars(before))}',
    after,
    ''
  ))
  ToDirectMode()
  TurnOffAbbr()
  RegisterToChainJisyo(after)
  RegisterToChainJisyo('')
enddef

def OnCmdlineEnter()
  # '@'ã‚‚å«ã‚ãŸã„ãŒredrawã§echoå‡ºåŠ›ãŒã‚¯ãƒªã‚¢ã•ã‚Œã¦ã—ã¾ã†
  if getcmdtype() =~# '[/?]'
    PopupMode()
  elseif getcmdtype() ==# ':'
    Disable(false)
  else
    ClosePopupWin()
    redraw
  endif
  if g:vim9skk_enable
    ToDirectMode()
    RunOnMidasi()
  endif
enddef

def OnCmdlineLeavePre()
  if g:vim9skk_enable
    ToDirectMode()
    CloseKouho()
    TurnOffAbbr()
  endif
enddef
# }}}

# å…¥åŠ›ãƒ¢ãƒ¼ãƒ‰åˆ¶å¾¡ {{{
var mode_settings_cache = {}
def GetModeSettings(m: number): any
  if !mode_settings_cache->has_key(m)
    mode_settings_cache[m] = CreateModeSettings(m)
  endif
  return mode_settings_cache[m]
enddef

def CreateModeSettings(m: number): any
  if m ==# MODE_HIRA
    return {
      id: MODE_HIRA,
      label: g:vim9skk.mode_label.hira,
      use_roman: true,
      items: roman_table_items,
      hi: 'vim9skkModeHira',
    }
  elseif m ==# MODE_KATA
    return {
      id: MODE_KATA,
      label: g:vim9skk.mode_label.kata,
      use_roman: true,
      items: roman_table_items
        ->ToItems((i) => [i[0], i[1]->ConvChars(hira_chars, kata_chars)]),
      hi: 'vim9skkModeKata',
    }
  elseif m ==# MODE_HANK
    return {
      id: MODE_HANK,
      label: g:vim9skk.mode_label.hankaku,
      use_roman: true,
      items: roman_table_items
        ->ToItems((i) => [i[0], i[1]->ConvChars(hira_chars, hankaku_chars)]),
      hi: 'vim9skkModeHankaku',
    }
  elseif m ==# MODE_ALPH
    return {
      id: MODE_ALPH,
      label: g:vim9skk.mode_label.alphabet,
      use_roman: false,
      items: abbr_chars
        ->ToItems((i) => [i, i->ConvChars(abbr_chars, alphabet_chars)]),
      hi: 'vim9skkModeAlphabet',
    }
  else
    return {
      id: MODE_ABBR,
      label: g:vim9skk.mode_label.abbr,
      use_roman: false,
      items: abbr_chars->ToItems((i) => [i, i]),
      hi: 'vim9skkModeAbbr',
    }
  endif
enddef

def SetMode(m: number)
  mode = GetModeSettings(m)
  MapDirectMode()
  if skkmode !=# SKKMODE_SELECT
    PopupMode()
  endif
  silent! doautocmd User Vim9skkModeChanged
enddef

def ToDirectMode(chain: string = '', delta: number = 0): string
  SetSkkMode(SKKMODE_DIRECT)
  start_pos = GetPos() - delta
  return chain
enddef

def RunOnMidasi(chain: string = ''): string
  if g:vim9skk.run_on_midasi
    U('')
  endif
  return chain
enddef

def SetSkkMode(s: number)
  if skkmode !=# s
    skkmode = s
    g:vim9skk_midasi = ''
    MapMidasiMode()
    if s ==# SKKMODE_MIDASI
      PopupMode()
      PopupColoredMidasi()
    else
      CloseColoredMidasi()
    endif
  endif
enddef

def ToggleMode(m: number): string
  var target = ''
  if skkmode ==# SKKMODE_MIDASI
    target = GetTarget()
    if !target
      ToDirectMode()
    endif
  endif
  # åŸºæœ¬ã¯ã‚«ã‚¿ã‚«ãƒŠãƒ¢ãƒ¼ãƒ‰ã«ã™ã‚‹ã ã‘
  if skkmode ==# SKKMODE_DIRECT
    SetMode(mode.id !=# m ? m : MODE_HIRA)
    return ''
  endif
  # è¦‹å‡ºã—ã‚„é¸æŠä¸­ãªã‚‰ã‚«ã‚¿ã‚«ãƒŠã«å¤‰æ›ã—ã¦ç¢ºå®šã™ã‚‹
  CloseKouho()
  const k_chars = m ==# MODE_KATA ? kata_chars : hankaku_chars
  const mm = GetModeSettings(m)
  const before = target->RemoveMarker()
  const after = before
    ->SwapChars(hira_chars, k_chars)
    ->ConvChars(kata_chars, k_chars)
    ->SwapChars(alphabet_chars, abbr_chars)
  RegisterToRecentJisyo(before, after)
  return after
    ->ReplaceTarget()
    ->ToDirectMode()
    ->RunOnMidasi()
    ->PopupMode()
enddef

def ToggleAbbr(): string
  if mode.id ==# MODE_ABBR
    TurnOffAbbr()
    if skkmode ==# SKKMODE_MIDASI || skkmode ==# SKKMODE_SELECT
      return Complete()
    endif
    TurnOffAbbr()
    return ''
  elseif skkmode ==# SKKMODE_MIDASI
    SetMode(MODE_ABBR)
    return ''
  elseif skkmode ==# SKKMODE_SELECT
    const c = Complete()
    SetMode(MODE_ABBR)
    return c .. SetMidasi()
  else
    SetMode(MODE_ABBR)
    return SetMidasi()
  endif
enddef

def TurnOffAbbr(): string
  if mode.id ==# MODE_ABBR
    UnmapAll()
    SetMode(MODE_HIRA)
  endif
  return ''
enddef
# }}}

# å…¥åŠ›ãƒ¢ãƒ¼ãƒ‰ã‚’ãƒãƒƒãƒ—ã‚¢ãƒƒãƒ— {{{
def PopupMode(s: string = ''): string
  g:vim9skk_mode = g:vim9skk_enable
    ? skkmode ==# SKKMODE_MIDASI && mode.id !=# MODE_ABBR
    ? g:vim9skk.mode_label.midasi
    : mode.label
    : g:vim9skk.mode_label.off
  ClosePopupWin()
  if !g:vim9skk_mode
    redraw
    return s
  endif
  var a = GetPopupWinPos()
  if !g:vim9skk_enable
    a.time = g:vim9skk.mode_label_timeout
  endif
  a.highlight = g:vim9skk_enable ? mode.hi : 'vim9skkModeOff'
  popupwin_winid = popup_create(g:vim9skk_mode, a)
  popupwin_kind = POPUPWIN_KIND_MODE
  redraw
  return s
enddef

def FollowCursorModePopupWin()
  if !popupwin_winid || popupwin_kind !=# POPUPWIN_KIND_MODE || !g:vim9skk_enable
    return
  endif
  if skkmode ==# SKKMODE_MIDASI && !!start_pos
      const p = GetPos()
      if p < start_pos
        SetMidasi()
        FixPosColoredMidasi()
      endif
  endif
  popup_move(popupwin_winid, GetPopupWinPos())
enddef
# }}}

# å¤‰æ›å¯¾è±¡ã‚’è‰²ä»˜ã‘ {{{
# TODO: ç”»é¢å³ç«¯ã§è¡¨ç¤ºãŒãŠã‹ã—ã„
def PopupColoredMidasi()
  if !popupwin_midasi
    popupwin_midasi_pos = GetPopupWinPos(0)
    popupwin_midasi_pos.highlight = 'vim9skkMidasi'
    popupwin_midasi = popup_create('', popupwin_midasi_pos)
  endif
  if !!popupwin_midasi_update_timer
    timer_stop(popupwin_midasi_update_timer)
  endif
  popupwin_midasi_update_timer = timer_start(20, UpdateColoredMidasi, { repeat: - 1 })
enddef

def FixPosColoredMidasi()
  if !!popupwin_midasi
    popupwin_midasi_pos = GetPopupWinPos(0)
    popupwin_midasi_pos.highlight = 'vim9skkMidasi'
    popup_move(popupwin_midasi, popupwin_midasi_pos)
  endif
enddef

def CloseColoredMidasi()
  if !!popupwin_midasi
    popup_close(popupwin_midasi)
    popupwin_midasi = 0
    timer_stop(popupwin_midasi_update_timer)
    popupwin_midasi_update_timer = 0
  endif
enddef

var latest_target = ''
def UpdateColoredMidasi(timer: number)
  if !!popupwin_midasi
    const t = GetTarget()
    if !!t
      popup_show(popupwin_midasi)
      popup_settext(popupwin_midasi, GetTarget())
    else
      popup_hide(popupwin_midasi)
    endif
    if latest_target !=# t
      if !t
        PopupMode()
      else
        ShowRecent(t)
      endif
      latest_target = t
    endif
  endif
enddef
# }}}

# ã‚­ãƒ¼å…¥åŠ› {{{
def EscapeForMap(key: string): string
  return key
    ->substitute('<', '<LT>', 'g')
    ->substitute('|', '<Bar>', 'g')
    ->substitute(' ', '<Space>', 'g')
    ->substitute('\', '<Bslash>', 'g')
enddef

def MapFunction(keys: any, f: string, enable: bool = true)
  for key in type(keys) ==# v:t_string ? [keys] : keys
    if !key
      # nop
    elseif enable
      # é¸æŠãƒ¢ãƒ¼ãƒ‰ã®xã‚„å…¨è‹±ãƒ¢ãƒ¼ãƒ‰ã®Lã«å¯¾å¿œã™ã‚‹ãŸã‚ã®é¢å€’ãªifæ–‡
      if skkmode ==# SKKMODE_SELECT || mode.use_roman || abbr_chars->Excludes(key)
        const nowait = len(key) ==# 1 ? '<nowait>' : ''
        execute $'map! <buffer> <script> {nowait} {key} <ScriptCmd>{f}->feedkeys("nit")<CR>'
      endif
    else
      silent! execute $'unmap! <buffer> <script> {key}'
      get(b:, 'vim9skk_saved_roman', {})->get(key, [])->ForEach((m) => mapset(m))
    endif
  endfor
enddef

# <buffer>ã«ãƒãƒƒãƒ”ãƒ³ã‚°ã—ãªã„ã¨ä»–ã®ãƒ—ãƒ©ã‚°ã‚¤ãƒ³ã«å–ã‚‰ã‚Œã¡ã‚ƒã†
def MapToBuf()
  if !g:vim9skk_enable
    return
  endif
  UnmapAll()
  b:vim9skk_saved_keymap = maplist()->filter((_, m) => m.buffer)
  MapDirectMode()
  MapMidasiMode()
  MapSelectMode(!!kouho)
enddef

def MapDirectMode()
  if get(b:, 'vim9skk_keymapped', 0) ==# mode.id
    return
  endif
  b:vim9skk_keymapped = mode.id
  MapRoman()
  # è¦‹å‡ºã—ãƒ¢ãƒ¼ãƒ‰ã‚„é¸æŠãƒ¢ãƒ¼ãƒ‰ã‹ã‚‰æˆ»ã£ã¦ããŸã¨ãã®ãŸã‚ã®ãƒãƒƒã‚¯ã‚¢ãƒƒãƒ—
  b:vim9skk_saved_roman = {}
  for key in g:vim9skk.keymap->values()->flattennew()
    b:vim9skk_saved_roman[key] = [
      maparg(key, 'i', false, true),
      maparg(key, 'c', false, true),
    ]
  endfor
  MapFunction(g:vim9skk.keymap.disable,  'Disable()')
  MapFunction(g:vim9skk.keymap.kata,     'ToggleMode(MODE_KATA)')
  MapFunction(g:vim9skk.keymap.hankaku,  'ToggleMode(MODE_HANK)')
  MapFunction(g:vim9skk.keymap.alphabet, 'ToggleMode(MODE_ALPH)')
  MapFunction(g:vim9skk.keymap.abbr,     'ToggleAbbr()')
  MapFunction(g:vim9skk.keymap.midasi,   'U("")') # å¤§æ–‡å­—ã‚’æŠ¼ã—ãŸã®ã¨åŒã˜
  # leximaãªã©ãŒinsertãƒ¢ãƒ¼ãƒ‰ã‚’è§£é™¤ã—ã¦ã—ã¾ã†ã®ã§â€¦
  noremap! <buffer> <script> <BS> <BS>
enddef

def MapRoman()
  if mode.use_roman
    for key in abbr_chars
      const k = key->EscapeForMap()
      silent! execute $'unmap! <buffer> <script> {k}'
    endfor
    for k in 'ABCDEFGHIJKMNOPRSTUVWXYZ'->split('.\zs')
      silent! execute $'unmap! <buffer> <script> {k->tolower()}'
      execute $'map! <buffer> <script> <nowait> {k} <ScriptCmd>U("{k}")->feedkeys("it")<CR>'
    endfor
  else
    # <nowait>ã§ã„ã‘ã‚‹ã‹ãªï¼Ÿã¨æ€ã£ãŸã‘ã©ã€ã¡ã‚ƒã‚“ã¨unmapã—ãªã„ã¨ãƒ†ã‚¹ãƒˆãŒé€šã‚‰ãªã„
    for [key, value] in roman_table_items
      const k = key->EscapeForMap()
      silent! execute $'unmap! <buffer> <script> {k}'
    endfor
  endif
  const map = mode.use_roman ? 'map!' : 'noremap! <nowait>'
  for [key, value] in mode.items
    const k = key->EscapeForMap()
    const v = value->escape('"|\\')
    # Note: feedkeysã ã¨windowsã§`ã›`ãŒæ–‡å­—åŒ–ã‘ã™ã‚‹
    execute $'{map} <buffer> <expr> <script> {k} vim9skk#L("{v}")'
  endfor
enddef

def MapMidasiMode()
  if g:vim9skk_enable
    const enable = skkmode !=# SKKMODE_DIRECT &&
      (skkmode !=# SKKMODE_MIDASI || !!g:vim9skk_midasi)
    MapFunction(g:vim9skk.keymap.select,   'StartSelect()', enable)
    MapFunction(g:vim9skk.keymap.complete, 'Complete()', enable)
    MapFunction(g:vim9skk.keymap.cancel,   'Select(-kouho_index)->Complete()', enable)
    MapFunction(g:vim9skk.keymap.prefix,   'SetPrefix()', enable)
  endif
enddef

def MapSelectMode(enable: bool)
  if g:vim9skk_enable
    MapFunction(g:vim9skk.keymap.next, 'Select(1)', enable)
    MapFunction(g:vim9skk.keymap.prev, 'Select(-1)', enable)
  endif
enddef

def UnmapAll()
  if !get(b:, 'vim9skk_keymapped', 0)
    return
  endif
  b:vim9skk_keymapped = 0
  for m in maplist()->filter((_, m) => m.script) # cmdlineã¯m.bufferç„¡ã—
    const lhs = m.lhs
      ->substitute('|', '<BAR>', 'g')
      ->substitute('\', '<Bslash>', 'g')
    silent! execute $'unmap! <buffer> <script> {lhs}'
  endfor
  for m in get(b:, 'vim9skk_saved_keymap', [])
    mapset(m)
  endfor
  b:vim9skk_saved_keymap = []
enddef

# å°æ–‡å­—å…¥åŠ›æ™‚(Lower)
export def L(chain: string): string
  var prefix = ''
  if skkmode ==# SKKMODE_SELECT
    prefix = Complete()
  elseif skkmode ==# SKKMODE_MIDASI
    GetTarget()
      ->Split(g:vim9skk.marker_okuri)[0]
      ->RemoveMarker()
      ->AddStr(chain)
      ->ShowRecent()
  endif
  if !kouho
    PopupMode()
  endif
  var v = chain
  if mode.use_roman  && v =~# '[a-z]$'
    feedkeys(v[-1], 'it')
    v = v[ : -2]
  endif
  return prefix .. v .. "\<ScriptCmd>vim9skk#MidasiInput()\<CR>"
enddef

# å¤§æ–‡å­—å…¥åŠ›æ™‚(Upper)
def U(key: string): string
  # é¸æŠãƒ¢ãƒ¼ãƒ‰ãªã‚‰ç¢ºå®šã™ã‚‹
  var comp = ''
  if skkmode ==# SKKMODE_SELECT
    comp = Complete()
  endif
  # ç›´æ¥å…¥åŠ›ãªã‚‰è¦‹å‡ºã—ãƒ¢ãƒ¼ãƒ‰ã¸é·ç§»ã™ã‚‹
  const target = GetTarget()
  if skkmode ==# SKKMODE_DIRECT
    return comp .. SetMidasi(key)
  endif
  # è¦‹å‡ºã—ãƒ¢ãƒ¼ãƒ‰ãªã‚‰â€¦
  var prefix = ''
  const sion = target->matchstr('[a-z]*$')
  if !!sion && !!key
    # ShiftæŠ¼ã—ã£ã±ãªã—ã§ã‚‚ãƒ­ãƒ¼ãƒå­—å…¥åŠ›ã§ãã‚‹ã‚ˆã†ã«é ‘å¼µã‚‹
    prefix = repeat("\<BS>", sion->len()) .. sion
  elseif !!target && target->stridx(g:vim9skk.marker_okuri) ==# -1
    # é€ã‚Šä»®åãƒãƒ¼ã‚«ãƒ¼ã‚’è¨­ç½®ã™ã‚‹
    prefix = g:vim9skk.marker_okuri
    # Note: <BS>ãªã©ã§ãƒãƒ¼ã‚«ãƒ¼ãŒå‰Šé™¤ã•ã‚Œã‚‹ã“ã¨ã‚’è€ƒæ…®ã™ã‚‹ã¨ã€é€ã‚Šä»®åã®ä½ç½®ã‚’å†…éƒ¨çš„ã«æŒã¤ã®ã¯é›£ã—ã‹ã£ãŸâ€¦
  endif
  return prefix .. key->tolower()
enddef

def SetMidasi(key: string = '', delta: number = 0): string
  SetSkkMode(SKKMODE_MIDASI)
  const next_start_pos = GetPos() - delta
  const next_word = GetLine()->matchstr($'\%{end_pos}c.*\%{next_start_pos}c')
  if !!next_word
    RegisterToChainJisyo(next_word)
  endif
  start_pos = next_start_pos
  return key->tolower()
enddef

export def MidasiInput()
  if skkmode ==# SKKMODE_MIDASI
    g:vim9skk_midasi = GetTarget()
    MapMidasiMode()
    DoUserEvent('Vim9skkMidasiInput')
  endif
enddef

def SetPrefix(): string
  if skkmode ==# SKKMODE_SELECT
    return Complete() .. SetMidasi(g:vim9skk.keymap.prefix, pos_delta)
  else
    return g:vim9skk.keymap.prefix
  endif
enddef
# }}}

# å¤‰æ› {{{
def GetTarget(): string
  return GetLine()->matchstr($'\%{start_pos}c.*\%{GetPos()}c')
enddef

def RemoveMarker(s: string): string
  return s
    ->substitute(g:vim9skk.marker_okuri, '', '')
enddef

def ReplaceTarget(after: string): string
  return "\<BS>"->repeat(strchars(GetTarget())) .. after
enddef

def StartSelect(chain: string = ''): string
  if skkmode ==# SKKMODE_SELECT
    return Select(1)
  endif
  const target = GetTarget()
  if !target
    return chain
  endif
  target->GetAllKouho()
  if !kouho
    CloseKouho()
    return ''
  endif
  SetSkkMode(SKKMODE_SELECT)
  PopupKouho(1)
  kouho_index = 0
  return Select(1)
enddef

def GetKouhoFromJisyo(path: string, key: string): list<string>
  const j = ReadJisyo(path)
  const head = $'{key} '->IconvTo(j.enc)
  const max = len(j.lines) - 1
  if max < 0
    return []
  endif
  var limit = g:vim9skk.search_limit
  var d = max
  var i = max / 2
  while !!limit
    limit -= 1
    const line = j.lines[i]
    if line->StartsWith(head)
      return line->IconvFrom(j.enc)->Split(' ')[1]->split('/')
    endif
    d = d / 2 + d % 2
    if d <= 1
      if !!limit
        # æ®‹ã‚Šã®æ¢ç´¢ãŒå¥‡æ•°å€‹ã ã¨å–ã‚Šæ¼ã‚‰ã™ã®ã§ã€ã‚ã¨1å›ã ã‘æ¢ç´¢ãŒã‚“ã°ã‚‹
        limit = 1
        d = 1
      else
        # ã‚‚ã†ã ã‚
        break
      endif
    endif
    i += line < head ? d : -d
    i = i < 0 ? 0 : max < i ? max : i
  endwhile
  return []
enddef

def GetAllKouho(target: string)
  if !target
    kouho = []
    return
  endif
  # `â–½ã»ã’*ãµãŒ`ã‚’è¦‹å‡ºã—ã¨é€ã‚Šä»®åã«åˆ†å‰²ã™ã‚‹
  const [m, o] = target
    ->Split(g:vim9skk.marker_okuri)
  kouho = [m] # å€™è£œä¸€ã¤ç›®ã¯ç„¡å¤‰æ›
  okuri = o # é€ã‚Šä»®åã¯å€™è£œé¸æŠæ™‚ã«ä½¿ã†ã®ã§ã‚¹ã‚¯ãƒªãƒ—ãƒˆãƒ­ãƒ¼ã‚«ãƒ«ã«ä¿æŒã—ã¦ãŠã
  # å€™è£œã‚’æ¤œç´¢ã™ã‚‹
  const midasi_key = m->ConvChars(kata_chars, hira_chars)
  const okuri_key = o
    ->ConvChars(kata_chars, hira_chars)
    ->substitute('^ã£*', '', '')
    ->matchstr('^.')
  henkan_key = $'{midasi_key}{okuri_table->get(okuri_key, '')}' # `ã»ã’f`
  for path in [g:vim9skk.jisyo_recent, g:vim9skk.jisyo_user] + g:vim9skk.jisyo
    kouho += GetKouhoFromJisyo(path, henkan_key)
  endfor
  kouho = kouho->Uniq()
  if len(kouho) ==# 1
    if m =~# '[ã‚”ãƒ¼ã±ã´ã·ãºã½]'
      kouho += [m->ConvChars(hira_chars, kata_chars)]
    else
      kouho = RegisterToUserJisyo(henkan_key)
    endif
  endif
enddef

def Cyclic(a: number, max: number): number
  return max ==# 0 ? 0 : ((a % max + max) % max)
enddef

def GetSelectedKouho(): string
  return kouho->get(kouho_index, '')->substitute(';.*', '', '')
enddef

def Select(d: number): string
  SetSkkMode(SKKMODE_SELECT)
  kouho_index = Cyclic(kouho_index + d, len(kouho))
  HighlightKouho()
  return ReplaceTarget($'{GetSelectedKouho()}{okuri}')
enddef

def AddLeftForParen(chain: string, p: string): string
  if g:vim9skk.parens->Includes(p)
    return chain .. "\<C-g>U\<Left>"
  else
    return chain
  endif
enddef

def Complete(chain: string = ''): string
  const before = GetTarget()
  const after = before->RemoveMarker()
  pos_delta = before->len() - after->len()
  RegisterToRecentJisyo(henkan_key, GetSelectedKouho())
  RegisterToChainJisyo(after)
  kouho = []
  henkan_key = ''
  ClosePopupWin()
  TurnOffAbbr()
  return chain ..
    after
      ->ReplaceTarget()
      ->AddLeftForParen(after)
      ->ToDirectMode(pos_delta)
      ->AfterComplete()
      ->RunOnMidasi()
enddef

def AfterComplete(chain: string): string
  ShowChainJisyo()
  if !kouho
    CloseKouho()
    PopupMode()
  endif
  return chain
enddef
# }}}

# å€™è£œã‚’ãƒãƒƒãƒ—ã‚¢ãƒƒãƒ— {{{
def PopupKouho(default: number = 0)
  MapSelectMode(!!kouho)
  ClosePopupWin()
  if !kouho
    redraw
    return
  endif
  if g:vim9skk.popup_maxheight <= 0
    return
  endif
  const target = GetTarget()
  const sp = screenpos(0, line('.'), col('.'))
  var popupwin_options = {
    pos: 'topleft',
    col: popupwin_midasi_pos.col,
    line: sp.row + 1,
    cursorline: true,
    maxheight: g:vim9skk.popup_maxheight,
    wrap: false,
  }
  if mode() ==# 'c'
    popupwin_options.line = popupwin_midasi_pos.line - 1
    popupwin_options.pos = 'botleft'
  elseif &lines - g:vim9skk.popup_minheight < screenrow()
    popupwin_options.line = sp.row - 1
    popupwin_options.pos = 'botleft'
  endif
  var lines = []
  for k in kouho
    const l = k->substitute(';', "\t", '')
    lines += [l]
  endfor
  popupwin_winid = popup_create(lines, popupwin_options)
  popupwin_kind = POPUPWIN_KIND_KOUHO
  win_execute(popupwin_winid, 'setlocal tabstop=12')
  win_execute(popupwin_winid, 'syntax match PMenuExtra /\t.*/')
  if default
    kouho_index = default
  endif
  HighlightKouho()
enddef

def HighlightKouho()
  if popupwin_winid !=# 0
    win_execute(popupwin_winid, $':{kouho_index + 1}')
    popup_setoptions(popupwin_winid, { cursorline: 0 <= kouho_index })
    redraw
  endif
enddef

def CloseKouho()
  MapSelectMode(false)
  if popupwin_kind ==# POPUPWIN_KIND_KOUHO
    ClosePopupWin()
  endif
  redraw
enddef
# }}}

# å¤‰æ›å±¥æ­´ã‚’ãƒãƒƒãƒ—ã‚¢ãƒƒãƒ— {{{
def AddDetail(list: list<string>, detail: string): list<string>
  var result = []
  for i in list
    result += [$'{i};{detail}']
  endfor
  return result
enddef

def ShowRecent(target: string)
  kouho = []
  const j = ReadRecentJisyo()
  const head = target->IconvTo(j.enc)
  for line in j.lines
    if line->StartsWith(head)
      kouho += line->IconvFrom(j.enc)->Split(' ')[1]->split('/')
    endif
  endfor
  if !len(kouho)
    CloseKouho()
  else
    kouho = kouho->Uniq()->AddDetail('å¤‰æ›å±¥æ­´')
    kouho_index = -1
    okuri = ''
    PopupKouho()
  endif
enddef
# }}}

# é€£é–è£œå®Œã‚’ãƒãƒƒãƒ—ã‚¢ãƒƒãƒ— {{{
# ğŸ§ªæ§˜å­è¦‹ä¸­
def RegisterToChainJisyo(next_word: string)
  if !!last_word && !!next_word
    chain_jisyo[last_word] = chain_jisyo->get(last_word, [])->insert(next_word)->Uniq()
  endif
  last_word = next_word
  end_pos = start_pos + next_word->len()
enddef

def ShowChainJisyo()
  if chain_jisyo->has_key(last_word)
    kouho = chain_jisyo[last_word]->AddDetail('å…¥åŠ›å±¥æ­´')
    kouho_index = -1
    PopupKouho()
  endif
enddef
# }}}

# è¾æ›¸æ“ä½œ {{{
def ToFullPathAndEncode(path: string): list<string>
  const m = path->matchlist('\(.\+\):\([a-zA-Z0-9-]*\)$')
  return !m ? [expand(path), ''] : [expand(m[1]), m[2]]
enddef

def IconvTo(str: string, enc: string): string
  return (!str || !enc || enc ==# &enc) ? str : str->iconv(&enc, enc)
enddef

def IconvFrom(str: string, enc: string): string
  return (!str || !enc || enc ==# &enc) ? str : str->iconv(enc, &enc)
enddef

export def ReadJisyo(path: string): dict<any>
  # ã‚­ãƒ£ãƒƒã‚·ãƒ¥æ¸ˆã¿
  if jisyo->has_key(path)
    return jisyo[path]
  endif
  # èª­ã¿è¾¼ã‚“ã§ã‚¹ã‚¯ãƒªãƒ—ãƒˆãƒ­ãƒ¼ã‚«ãƒ«ã«ã‚­ãƒ£ãƒƒã‚·ãƒ¥ã™ã‚‹
  const [p, enc] = ToFullPathAndEncode(path)
  if !filereadable(p)
    # å¾Œã‹ã‚‰è¾æ›¸ãƒ•ã‚¡ã‚¤ãƒ«ã‚’ç½®ã‹ã‚Œã‚‹å¯èƒ½æ€§ãŒã‚ã‚‹ã®ã§ã€ã‚­ãƒ£ãƒƒã‚·ãƒ¥ã—ãªã„
    return { lines: [], enc: enc }
  endif
  # iconvã¯Windowsã§ã™ã”ãé‡ã„ã®ã§ã€èª­ã¿è¾¼ã¿æ™‚ã«ã¯å…¨ä½“ã‚’å¤‰æ›ã—ãªã„
  # æ¤œç´¢æ™‚ã«æ¤œç´¢å¯¾è±¡ã®æ–¹ã®æ–‡å­—ã‚³ãƒ¼ãƒ‰ã‚’è¾æ›¸ã«ã‚ã‚ã›ã‚‹
  jisyo[path] = { lines: readfile(p)->sort(), enc: enc }
  return jisyo[path]
enddef

def WriteJisyo(lines: list<string>, path: string, flags: string = '')
  const [p, _] = ToFullPathAndEncode(path)
  writefile(lines, p, flags)
enddef

export def RegisterToUserJisyo(key: string): list<string>
  if is_registering_user_jisyo
    return []
  endif
  is_registering_user_jisyo = true
  const save = {
    mode_id: mode.id,
    skkmode: skkmode,
    start_pos: start_pos,
    end_pos: end_pos,
    okuri: okuri,
    popupwin_midasi_pos: popupwin_midasi_pos->deepcopy(),
  }
  var result = []
  try
    SetSkkMode(SKKMODE_DIRECT)
    autocmd vim9skk CmdlineEnter * ++once PopupMode()
    const value = input($'ãƒ¦ãƒ¼ã‚¶ãƒ¼è¾æ›¸ã«ç™»éŒ²({key}): ')->trim()
    if !value
      echo 'ã‚­ãƒ£ãƒ³ã‚»ãƒ«ã—ã¾ã—ãŸ'
    else
      # ãƒ¦ãƒ¼ã‚¶ãƒ¼è¾æ›¸ã«ç™»éŒ²ã™ã‚‹
      const newline = $'{key} /{value}/'
      var j = ReadJisyo(g:vim9skk.jisyo_user)
      jisyo[g:vim9skk.jisyo_user] = j # ãƒ¦ãƒ¼ã‚¶ãƒ¼è¾æ›¸ãƒ•ã‚¡ã‚¤ãƒ«ãŒç„¡ã„å ´åˆã«å‚™ãˆã¦ã‚­ãƒ£ãƒƒã‚·ãƒ¥ã‚’ä¸Šæ›¸ãã™ã‚‹
      jisyo[g:vim9skk.jisyo_user].lines += [newline->IconvTo(j.enc)]
      [newline]->WriteJisyo(g:vim9skk.jisyo_user, 'a')
      result += [value]
      echo 'ç™»éŒ²ã—ã¾ã—ãŸ'
    endif
  finally
    SetMode(save.mode_id)
    SetSkkMode(save.skkmode)
    start_pos = save.start_pos
    end_pos = save.end_pos
    okuri = save.okuri
    popupwin_midasi_pos = save.popupwin_midasi_pos
    is_registering_user_jisyo = false
  endtry
  return result
enddef

def ReadRecentJisyo(): dict<any>
  if !recent_jisyo
    const [p, enc] = ToFullPathAndEncode(g:vim9skk.jisyo_recent)
    if !filereadable(p)
      return { lines: [], enc: enc }
    endif
    recent_jisyo = { lines: readfile(p), enc: enc }
  endif
  return recent_jisyo
enddef

def RegisterToRecentJisyo(before: string, after: string)
  if !before || !after
    return
  endif
  # æ–°è¦ã«è¿½åŠ ã™ã‚‹è¡Œ
  const afters = GetKouhoFromJisyo(g:vim9skk.jisyo_recent, before)
    ->insert(after)
    ->Uniq()
    ->join('/')
  const newline = $'{before} /{afters}/'
  # æ—¢å­˜ã®è¡Œã‚’å‰Šé™¤ã—ã¦ã‹ã‚‰å…ˆé ­ã«è¿½åŠ ã™ã‚‹
  var j = ReadRecentJisyo()
  const head = $'{before} '->IconvTo(j.enc)
  j.lines = j.lines # å†ä»£å…¥ã—ãªã„ã¨ã ã‚ï¼Ÿï¼Ÿ
    ->filter((_, v) => !v->StartsWith(head))
    ->slice(0, g:vim9skk.recent)
    ->insert(newline->IconvTo(j.enc))
  # å€™è£œæ¢ç´¢ç”¨ã®è¾æ›¸ã«ã¯ã‚½ãƒ¼ãƒˆæ¸ˆã®ã‚‚ã®ã‚’ã‚»ãƒƒãƒˆã™ã‚‹
  jisyo[g:vim9skk.jisyo_recent] = { lines: j.lines->copy()->sort(), enc: j.enc }
enddef

def SaveRecentJisyo()
  var lines = ReadRecentJisyo().lines
  if !!lines
    lines->WriteJisyo(g:vim9skk.jisyo_recent)
  endif
enddef

export def RefreshJisyo()
  jisyo = {}
  recent_jisyo = {}
  echo 'è¾æ›¸ã‚’ãƒªãƒ•ãƒ¬ãƒƒã‚·ãƒ¥ã—ã¾ã—ãŸ'
enddef
# }}}

# terminal {{{
export def TerminalInput()
  autocmd CmdlineEnter * ++once Enable()
  const value = input($'terminalã«å…¥åŠ›: ')->trim()
  if !!value
    feedkeys(value, 'int')
  endif
enddef
# }}}

